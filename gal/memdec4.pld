GAL22V10 ; Same JED output can be flashed to ATF22V10.
memdec4  ; Flasher needs to support ATF22V10 algorithms

; Pins: pin names beginning with "N" are active low.
CLK NUDS NLDS NAS  A23  A22  A21  P08    P09 P10 P11 GND
P13 CEM1 CEM2 CEM3 CEM4 NCEL NCEH NDTACK P21 P22 P23 VCC

; Equations:

; SRAM Chip Enables
; Each chip holds 2MB of data. Address only valid on /NAS
; Lines are activated at these bus addresses:
CEM1    =  A21 * /A22 * /A23 * /NAS ;0x200000-0x3ffffe
CEM2    = /A21 *  A22 * /A23 * /NAS ;0x400000-0x5ffffe

; DTACK
; Ground the line as soon as we have a valid address
/NDTACK =  A21 * /A22 * /A23 * /NAS
        + /A21 *  A22 * /A23 * /NAS

; DATA bus drivers:
; Enable when we have a valid address and the right byte
/NCEH   =  A21 * /A22 * /A23 * /NAS * /NUDS
        + /A21 *  A22 * /A23 * /NAS * /NUDS

/NCEL   =  A21 * /A22 * /A23 * /NAS * /NLDS
        + /A21 *  A22 * /A23 * /NAS * /NLDS

DESCRIPTION

Address decoder for Sharp X68000 SRAM board.
Board holding 4MB on a 2MB built-in machine.

Tested on a X68000 Expert system.

; vim:syntax=off:
