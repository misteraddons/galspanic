GAL22V10 ; Same JED output can be flashed to ATF22V10.
memdec   ; Flasher needs to support ATF22V10 algorithms

; Pins: pin names beginning with "N" are active low.
; MS4, MS6 and MS8 set SRAM chip presence
CLK NUDS NLDS NAS  A23  A22  A21  A20    P09 P10 P11 GND
MSK CEM1 CEM2 CEM3 CEM4 NCEL NCEH NDTACK MS4 MS6 MS8 VCC

; Equations:

; SRAM Chip Enables
; Each chip holds 2MB of data. Address only valid on /NAS
; Lines are activated at these bus addresses:
CEM1    =        A21 * /A22 * /A23 * /NAS ;0x200000-0x3ffffe
CEM2    = MS4 * /A21 *  A22 * /A23 * /NAS ;0x400000-0x5ffffe
CEM3    = MS6 *  A21 *  A22 * /A23 * /NAS ;0x600000-0x7ffffe
CEM4    = MS8 * /A21 * /A22 *  A23 * /NAS ;0x800000-0x9ffffe

; DTACK
; Ground the line as soon as we have a valid address
/NDTACK =        A21 * /A22 * /A23 * /NAS
        + MS4 * /A21 *  A22 * /A23 * /NAS
        + MS6 *  A21 *  A22 * /A23 * /NAS
        + MS8 * /A21 * /A22 *  A23 * /NAS

; DATA bus drivers:
; Enable when we have a valid address and the right byte
/NCEH   =        A21 * /A22 * /A23 * /NAS * /NUDS
        + MS4 * /A21 *  A22 * /A23 * /NAS * /NUDS
        + MS6 *  A21 *  A22 * /A23 * /NAS * /NUDS
        + MS8 * /A21 * /A22 *  A23 * /NAS * /NUDS

/NCEL   =        A21 * /A22 * /A23 * /NAS * /NLDS
        + MS4 * /A21 *  A22 * /A23 * /NAS * /NLDS
        + MS6 *  A21 *  A22 * /A23 * /NAS * /NLDS
        + MS8 * /A21 * /A22 *  A23 * /NAS * /NLDS

DESCRIPTION

Address decoder for Sharp X68000 SRAM board.
Jumpers on pins MS4, MS6 and MS8 set SRAM chip presence.
Board can hold up to 8MB on a 2MB built-in machine.

See project README.md for more info.

; vim:syntax=off:
